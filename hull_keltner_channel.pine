// Keltner Channel using @insilico's Hull Suite as Moving Average source, with 2 pairs of envelops.
//@version=4

study(title="Hull Keltner Channel", shorttitle="HKCB", overlay=true)

// HULL INPUT
src = input(close, title="Hull's Source")
modeSwitch = input("Hma", title="Hull Variation", options=["Hma", "Thma", "Ehma"])
lengthHull = input(55, title="Length(180-200 for floating S/R , 55 for swing entry)")
lengthMult = input(1.0, title="Length multiplier (Used to view higher timeframes with straight band)")

useHtf = input(false, title="Show Hull MA from X timeframe? (good for scalping)")
htf = input("240", title="Higher timeframe", type=input.resolution)

switchColor = input(true, "Color Hull according to trend?")
candleCol = input(false,title="Color candles based on Hull's Trend?")
visualSwitch  = input(true, title="Show as a Band?")
thicknesSwitch = input(1, title="Line Thickness")
transpSwitch = input(40, title="Band Transparency",step=5)

// HULL FUNCTIONS

//HMA
HMA(_src, _length) =>  wma(2 * wma(_src, _length / 2) - wma(_src, _length), round(sqrt(_length)))
//EHMA    
EHMA(_src, _length) =>  ema(2 * ema(_src, _length / 2) - ema(_src, _length), round(sqrt(_length)))
//THMA    
THMA(_src, _length) =>  wma(wma(_src,_length / 3) * 3 - wma(_src, _length / 2) - wma(_src, _length), _length)
    
//SWITCH
Mode(modeSwitch, src, len) =>
      modeSwitch == "Hma"  ? HMA(src, len) :
      modeSwitch == "Ehma" ? EHMA(src, len) : 
      modeSwitch == "Thma" ? THMA(src, len/2) : na

//OUT
_hull = Mode(modeSwitch, src, int(lengthHull * lengthMult))
HULL = useHtf ? security(syminfo.ticker, htf, _hull) : _hull
MHULL = HULL[0]
SHULL = HULL[2]

//COLOR
hullColor = switchColor ? (HULL > HULL[2] ? #00ff00 : #ff0000) : #ff9800

//PLOT
///< Frame
Fi1 = plot(MHULL, title="MHULL", color=hullColor, linewidth=thicknesSwitch, transp=50)
Fi2 = plot(visualSwitch ? SHULL : na, title="SHULL", color=hullColor, linewidth=thicknesSwitch, transp=50)
///< Ending Filler
fill(Fi1, Fi2, title="Band Filler", color=hullColor, transp=transpSwitch)
///BARCOLOR
barcolor(color = candleCol ? (switchColor ? hullColor : na) : na)

// KELTNER BAND

useATR = input(true)
multiplier = input(2.0)
multiplier2 = input(4.0)
length = input(200, minval=1)

pine_atr(length) =>
    h = useHtf ? security(syminfo.ticker, htf, high) : high
    l = useHtf ? security(syminfo.ticker, htf, low) : low
    c = useHtf ? security(syminfo.ticker, htf, close[1]) : close[1]
    trueRange = na(h[1])? h-l : max(max(h - l, abs(h - c)), abs(l - c))
    rma(trueRange, length)

ma = HULL
range = useATR ? pine_atr(length) : high - low
rangema = ema(range, length)
upper = ma + rangema * multiplier
lower = ma - rangema * multiplier
upper2 = ma + rangema * multiplier2
lower2 = ma - rangema * multiplier2

u = plot(upper, color=color.orange, title="Upper 1", transp=50)
u2 = plot(upper2, color=color.red, title="Upper 2", transp=50)
plot(ma, color=color.black, title="Moving Average")
l = plot(lower, color=color.orange, title="Lower 1", transp=50)
l2 = plot(lower2, color=color.red, title="Lower 2", transp=50)
fill(u, l, color=color.purple, transp=95)
fill(u2, l2, color=color.purple, transp=90)
